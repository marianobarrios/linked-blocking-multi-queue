plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
    id "com.diffplug.spotless" version "7.0.3"
}

var javaToolchain = System.getenv('JAVA_TOOLCHAIN')

java {
    if (javaToolchain != null) {
        toolchain {
            languageVersion = JavaLanguageVersion.of(javaToolchain)
        }
    }
}

compileJava {
    if (javaToolchain != "8") {
        options.release = 8
    }
    options.compilerArgs.add('-Xlint')
}

compileTestJava {
    if (javaToolchain != "8") {
        options.release = 8
    }
    options.compilerArgs.add('-Xlint')
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

spotless {
    java {
        // remove when spotless moved to this Palantir version
        palantirJavaFormat("2.38.0")
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        lbmq(MavenPublication) {
            groupId = 'com.github.marianobarrios'
            artifactId = 'lbmq'
            version = '0.7.0'
            from components.java
            pom {
                name = 'Linked Blocking Multi-Queue'
                description = 'A concurrent collection that extends the existing Java concurrent collection library, ' +
                        'offering an optionally-bounded blocking "multi-queue" based on linked nodes.'
                url = 'https://github.com/marianobarrios/linked-blocking-multi-queue'
                licenses {
                    license {
                        name = 'BSD-style'
                        url = 'http://www.opensource.org/licenses/bsd-license.php'
                    }
                }
                developers {
                    developer {
                        name = 'Mariano Barrios'
                        email = 'marbar@gmail.com'
                    }
                }
                scm {
                    url = 'scm:git@github.com:marianobarrios/lbmq.git'
                    connection = 'scm:git@github.com:marianobarrios/lbmq.git'
                    developerConnection = 'scm:git@github.com:marianobarrios/lbmq.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.findProperty('sonatypeUsername')
                password = project.findProperty('sonatypePassword')
            }
        }
    }
}

signing {
    sign publishing.publications.lbmq
}
