plugins {
    id 'java'
    id 'jvm-test-suite'
    id 'signing'
    id 'maven-publish'
    id "com.diffplug.spotless" version "8.2.1"
}

compileJava {
    options.release = 8
    // for some reason javac warns the previous option, disabling
    options.compilerArgs.add('-Xlint:all,-options')
}

compileTestJava {
    options.release = 8
    // for some reason javac warns the previous option, disabling
    options.compilerArgs.add('-Xlint:all,-options')
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.2'
}

java {
    withSourcesJar()
    withJavadocJar()
}

var testJavaToolchain = System.getenv('TEST_JAVA_TOOLCHAIN')

testing {
    suites {
        test {
            useJUnitJupiter()
            targets {
                all {
                    testTask.configure {
                        if (testJavaToolchain != null) {
                            javaLauncher.set(
                                    javaToolchains.launcherFor {
                                        languageVersion.set(JavaLanguageVersion.of(testJavaToolchain))
                                    }
                            )
                        }
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        lbmq(MavenPublication) {
            groupId = 'com.github.marianobarrios'
            artifactId = 'lbmq'
            version = '0.7.0'
            from components.java
            pom {
                name = 'Linked Blocking Multi-Queue'
                description = 'A concurrent collection that extends the existing Java concurrent collection library, ' +
                        'offering an optionally-bounded blocking "multi-queue" based on linked nodes.'
                url = 'https://github.com/marianobarrios/linked-blocking-multi-queue'
                licenses {
                    license {
                        name = 'BSD-style'
                        url = 'http://www.opensource.org/licenses/bsd-license.php'
                    }
                }
                developers {
                    developer {
                        name = 'Mariano Barrios'
                        email = 'marbar@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/marianobarrios/linked-blocking-multi-queue'
                    connection = 'scm:git@github.com:marianobarrios/linked-blocking-multi-queue.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.findProperty('sonatypeUsername')
                password = project.findProperty('sonatypePassword')
            }
        }
    }
}

signing {
    sign publishing.publications.lbmq
}
