plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
    id "com.diffplug.spotless" version "6.1.0"
}

compileJava {
    if (JavaVersion.current().ordinal() > JavaVersion.VERSION_1_8.ordinal()) {
        options.compilerArgs.addAll(['--release', '8'])
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

spotless {
    java {
        googleJavaFormat()
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        lbmq(MavenPublication) {
            groupId = 'com.github.marianobarrios'
            artifactId = 'lbmq'
            version = '0.6.0-SNAPSHOT'
            from components.java
            pom {
                name = 'Linked Blocking Multi-Queue'
                description = 'A concurrent collection that extends the existing Java concurrent collection library, ' +
                        'offering an optionally-bounded blocking "multi-queue" based on linked nodes.'
                url = 'https://github.com/marianobarrios/linked-blocking-multi-queue'
                licenses {
                    license {
                        name = 'BSD-style'
                        url = 'http://www.opensource.org/licenses/bsd-license.php'
                    }
                }
                developers {
                    developer {
                        name = 'Mariano Barrios'
                        email = 'marbar@gmail.com'
                    }
                }
                scm {
                    url = 'scm:git@github.com:marianobarrios/lbmq.git'
                    connection = 'scm:git@github.com:marianobarrios/lbmq.git'
                    developerConnection = 'scm:git@github.com:marianobarrios/lbmq.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.findProperty('sonatypeUsername')
                password = project.findProperty('sonatypePassword')
            }
        }
    }
}

signing {
    sign publishing.publications.lbmq
}
